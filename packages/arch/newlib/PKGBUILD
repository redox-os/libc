_target=x86_64-unknown-redox
pkgname=$_target-newlib-git
pkgver=r17774.d5ac42a49
pkgrel=1
arch=(i686 x86_64)
license=(GPL)
source=("git+https://github.com/redox-os/newlib#branch=redox")
md5sums=('SKIP')
makedepends=('git' 'rustup' $_target-binutils-git $_target-gcc-freestanding-git)

prepare() {
  cd "$srcdir/newlib"

  rustup override set nightly-2018-03-26
  rustup component add rust-src

  rm -rf $srcdir/xargo
  cargo install --root $srcdir/xargo xargo

  rm -rf $srcdir/newlib-build
  mkdir $srcdir/newlib-build
}

build() {
  cd "$srcdir/newlib-build"
  $srcdir/newlib/configure --target=$_target --prefix=/usr --enable-newlib-iconv
  PATH=$srcdir/xargo/bin:$PATH make all
}

package() {
  cd "$srcdir/newlib-build"
  make DESTDIR="$pkgdir" install
}

pkgver() {
  cd "$srcdir/newlib"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
