_target=x86_64-elf-redox
pkgname=$_target-newlib-git
pkgver=r17665.a3025b20e
pkgrel=1
arch=(i686 x86_64)
license=(GPL)
source=("git+https://github.com/redox-os/newlib#branch=redox" "git+https://github.com/redox-os/rust")
md5sums=('SKIP' 'SKIP')
makedepends=('git' 'rustup' $_target-binutils-git $_target-gcc-freestanding-git)

prepare() {
  cd rust
  git submodule update --init src/compiler-rt src/liblibc

  cd "$srcdir/newlib"

  rustup override set nightly
  rustup update nightly

  rm -rf $srcdir/xargo
  cargo install --root $srcdir/xargo xargo

  rm -rf $srcdir/newlib-build
  mkdir $srcdir/newlib-build
}

build() {
  cd "$srcdir/newlib-build"
  $srcdir/newlib/configure --target=$_target --prefix=/usr
  PATH=$srcdir/xargo/bin:$PATH XARGO_RUST_SRC="$srcdir/rust/src" make all
}

package() {
  cd "$srcdir/newlib-build"
  make DESTDIR="$pkgdir" install
}

pkgver() {
  cd "$srcdir/newlib"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
